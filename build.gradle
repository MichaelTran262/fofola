buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.1.6.RELEASE'
        classpath 'com.bmuschko:gradle-docker-plugin:4.10.0'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.bmuschko.docker-remote-api'

group = 'cz.mzk'
version = '1.0.4'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

bootJar {
    mainClassName = 'cz.mzk.fofola.Application'
    destinationDir = project.file('docker/build')
}

dependencies {
    compile('commons-codec:commons-codec:1.10')
    compile('com.squareup.okhttp:okhttp:2.4.0')
    compile('org.postgresql:postgresql:42.2.6')
    compile('com.sun.jersey:jersey-client:1.19.4')
    compile('com.squareup.retrofit:retrofit:1.9.0')
    compile('org.glassfish.jersey.core:jersey-client:2.28')
    compile('com.google.code.gson:gson:2.8.5')

    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-websocket')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.boot:spring-boot-starter-data-solr')

    def withoutAxonServer = { exclude group: 'org.axonframework', module: 'axon-server-connector' }
    compile group: 'org.axonframework', name: 'axon-spring-boot-starter', version: '4.3.1', withoutAxonServer

    compile("org.webjars:jquery:3.1.0")
    compile("org.webjars:bootstrap:3.3.7")
    compile("org.webjars:sockjs-client:1.0.2")
    compile("org.webjars:webjars-locator-core")
    compile("org.webjars:stomp-websocket:2.3.3")

    testCompile group: 'junit', name: 'junit', version: '4.12'
}


/********************* DOCKER BUILD ***********************/

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

task createDockerfile(type: Dockerfile) {
    dependsOn bootJar

    destFile.set(project.file('docker/build/Dockerfile'))
    from('openjdk:jre-alpine')
    label(['maintainer': 'Aleksei Ermak "aleksei.ermak@gmail.com"'])
    copyFile(jar.archiveName, '/app/' + jar.archiveName)
    runCommand('apk --update --no-cache add curl nmap')
    entryPoint('java')
    exposePort(8081)
    defaultCommand('-Xmx2g', '-jar', '/app/' + jar.archiveName)
}

task buildImage(type: DockerBuildImage) {
    dependsOn createDockerfile

    inputDir = project.file('docker/build')
    tags.add('ermak/fofola:' + version)
}

clean.doFirst {
    delete(project.file('docker/build'))
    delete(project.file('logs'))
}
