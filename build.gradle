buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.1.6.RELEASE'
        classpath 'com.bmuschko:gradle-docker-plugin:6.6.1'
    }
}

plugins {
    id 'com.bmuschko.docker-remote-api' version '6.6.1'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'cz.mzk'
version = '1.0.9'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

bootJar {
    mainClassName = 'cz.mzk.fofola.Application'
    destinationDir = project.file('docker/build')
}

dependencies {
    compile('commons-codec:commons-codec:1.10')
    compile('com.squareup.okhttp:okhttp:2.4.0')
    compile('org.postgresql:postgresql:42.2.6')
    compile('com.sun.jersey:jersey-client:1.19.4')
    compile('com.squareup.retrofit:retrofit:1.9.0')
    compile('org.glassfish.jersey.core:jersey-client:2.28')
    compile('com.google.code.gson:gson:2.8.5')
    compile('org.apache.solr:solr-solrj:8.6.3')
    compile('org.apache.commons:commons-lang3:3.11')
    compile('com.h2database:h2:1.4.200')
    compile('org.projectlombok:lombok:1.18.12')

    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')

    def withoutAxonServer = { exclude group: 'org.axonframework', module: 'axon-server-connector' }
    compile group: 'org.axonframework', name: 'axon-spring-boot-starter', version: '4.3.1', withoutAxonServer

    annotationProcessor('org.projectlombok:lombok:1.18.12')
}

clean.doFirst {
    delete(project.file('docker/build'))
    delete(project.file('logs'))
    delete(project.file('check_donator_out'))
}

/********************* DOCKER BUILD ***********************/

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerTagImage

task createDockerfile(type: Dockerfile) {
    dependsOn bootJar

    destFile.set(project.file('docker/build/Dockerfile'))
    from('openjdk:jre-alpine')
    label(['maintainer': 'Aleksei Ermak "aleksei.ermak@gmail.com"'])
    copyFile(jar.archiveName, '/app/' + jar.archiveName)
    runCommand('apk --update --no-cache add curl nmap')
    entryPoint('java')
    exposePort(8081)
    defaultCommand('-Xmx2g', '-jar', '/app/' + jar.archiveName)
}

task buildImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = project.file('docker/build')
    images.add('ermak/fofola:' + version)
}

task buildLatest(type: DockerTagImage) {
    dependsOn buildImage

    imageId = buildImage.getImageId()
    repository = 'ermak/fofola'
    tag = 'latest'
}

